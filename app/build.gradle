apply plugin: 'com.android.application'

/**
 * basic settings to have different flavors
 */
def debugSuffix = System.getProperty('debugSuffix', project.getProperties().get('debugSuffix', ''))
def final theApplicationId = "de.beusterse.abfallkalenderlandkreisrostock"

android {
    namespace 'de.beusterse.abfalllro'
    signingConfigs {
        release
    }
    compileSdk 35
    defaultConfig {
        applicationId theApplicationId
        minSdkVersion 23
        targetSdkVersion 35
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        versionCode 30
        versionName "1.8.4"
    }
    flavorDimensions "app"
    productFlavors {
        dev {
            buildConfigField "String", "API_SUFFIX", "\"_dev\""
            applicationId = theApplicationId + ".dev" + debugSuffix
        }
        full {
            buildConfigField "String", "API_SUFFIX", "\"\""
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    buildFeatures {
        buildConfig true
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'org.mockito:mockito-core:1.10.19'
}

/**
 * loading signing data (keystore, passwords, alias) if the
 * keystore.properties file in the root directory can be read
 */
Properties props = new Properties()

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

if (file('../keystore.properties').exists()) {
    props.load(new FileInputStream(file('../keystore.properties')))

    if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD')
            && props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']

    } else {
        println 'keystore.properties found, but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'keystore.properties not found'
    android.buildTypes.release.signingConfig = null
}
